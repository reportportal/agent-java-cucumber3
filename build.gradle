/*
 * Copyright 2020 EPAM Systems
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'java-library'

project.ext.releaseMode = project.hasProperty('releaseMode')

def branch = releaseMode ? '5.0.0' : 'develop'
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/build-quality.gradle"
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/release-commons.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
    maven { url "http://dl.bintray.com/epam/reportportal" }
    maven { url "https://jitpack.io" }
}

project.ext.githubUserName = project.hasProperty('githubUserName') ? githubUserName : ""
project.ext.githubToken = project.hasProperty('githubToken') ? githubToken : ""

publishing {
    repositories {
        maven {
            name = "Cucumber3 GitHub Packages"
            url = uri("https://maven.pkg.github.com/reportportal/agent-java-cucumber3")
            credentials {
                username = githubUserName
                password = githubToken
            }
        }
    }
}

dependencies {
    api 'com.epam.reportportal:client-java:5.0.12'
    api 'com.epam.reportportal:commons-model:5.0.0'
    api 'com.google.code.findbugs:jsr305:3.0.2'

    api 'io.cucumber:cucumber-java:3.0.2'

    testImplementation('com.github.reportportal:agent-java-test-utils:ddcf50ee20')

    testImplementation 'org.aspectj:aspectjweaver:1.9.2'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation 'org.mockito:mockito-core:3.3.3'
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'io.cucumber:cucumber-testng:3.0.2'
    testImplementation 'com.epam.reportportal:logger-java-logback:5.0.3'

    testImplementation ('org.junit.platform:junit-platform-runner:1.6.2') {
        exclude module: 'junit'
    }
    testImplementation 'org.junit.platform:junit-platform-launcher:1.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.3.3'
    testImplementation 'org.apache.commons:commons-io:1.3.2'
}

test {
    outputs.upToDateWhen { return false }
    useJUnitPlatform()
    maxParallelForks(5) // it's forks - separate JVMs, should not interfere each other
    doFirst {
        def weaver = configurations.testRuntimeClasspath.find { it.name.contains("aspectjweaver") }
        jvmArgs += "-javaagent:$weaver"
    }
}


wrapper {
    gradleVersion = '5.4.1'
}

processResources {
    filesMatching('agent.properties') {
        expand(project.properties)
    }
}

def releaseDependencies = [bintrayUpload, publish]
releaseDependencies.addAll(afterReleaseBuild.getDependsOn())
afterReleaseBuild.setDependsOn(releaseDependencies)
